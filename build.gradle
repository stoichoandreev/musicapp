buildscript {
    ext.kotlin_plugin_version = "1.2.71"
    ext.build_tools_plugin_version = "3.2.0"
    ext.dexcount_plugin_version = '0.8.3'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$build_tools_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_plugin_version"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$dexcount_plugin_version"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC6-3")
}

def versionProperties = new Properties()
file("version.properties").withInputStream { versionProperties.load(it) }
project.ext.versionMajor = versionProperties.getProperty("versionMajor")
project.ext.versionMinor = versionProperties.getProperty("versionMinor")
project.ext.versionPatch = versionProperties.getProperty("versionPatch")
project.ext.versionCode = versionProperties.getProperty("versionCode")
project.ext.versionName = "${project.ext.versionMajor}.${project.ext.versionMinor}.${project.ext.versionPatch}"

allprojects {
    repositories {
        google()
        jcenter()
    }
}

repositories {
    mavenCentral()
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    version = "1.0.0.RC6-3"
    profile("main") {
        config = "$projectDir/default-detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

apply from: 'applinks.gradle'
apply from: 'dependencies.gradle'
